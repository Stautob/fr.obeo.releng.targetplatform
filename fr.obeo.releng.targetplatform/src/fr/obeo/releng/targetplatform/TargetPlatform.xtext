grammar fr.obeo.releng.targetplatform.TargetPlatform with org.eclipse.xtext.common.Terminals

import "fr.obeo.releng.targetplatform"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
											
TargetPlatform:
	(
		'target' name=STRING
		contents+=TargetContent*
	)?
;

TargetContent: 
	Options | Environment | IncludeDeclaration | Location | VarDefinition
;

Options: 
	'with' options+=Option (','? options+=Option)*
;

Environment:
	{Environment}
	'environment' env+=ID (','? env+=ID)*
;

VarDefinition:
	'define' name=ID '=' value=STRING
;

CompositeString:
	{CompositeString}
	stringParts+=CompositeStringPart*
;

CompositeStringPart:
	VarCall | StaticString
;

VarCall:
	"${" varName=[VarDefinition] "}"
;

StaticString:
	value=STRING
;

IncludeDeclaration:
	'include' compositeImportURI=CompositeString
;

Location:
	'location' ((ID=ID)? & compositeUri=CompositeString) ('{'
		('with' options+=Option (','? options+=Option)*)?
		ius+=IU*
	'}')?
;

enum Option:
	INCLUDE_REQUIRED |
	INCLUDE_ALL_ENVIRONMENTS |
	INCLUDE_SOURCE |
	INCLUDE_CONFIGURE_PHASE
;

IU:
	ID=ID ( (';' 'version' '=')? (definableVersion=DefinableVersion) )? 
;

DefinableVersion:
	VersionRange | StaticString | VarCall
;

terminal VERSION: INT ('.' INT ('.' INT ('.' (ID|INT|QUALIFIER))?)?)?;
terminal VRANGE: ('('|'[') (' '*) VERSION (' '*) ',' (' '*) VERSION (' '*) (')'|']');

VersionRange hidden (WS):
	{VersionRange}
	range=(VRANGE | VERSION | 'lazy')
;

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('.'? ('a'..'z'|'A'..'Z'|'^'|'_'|'-'|'0'..'9'))*;
terminal QUALIFIER: ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
